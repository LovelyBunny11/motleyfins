plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

group = mod_group_id

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }
    mods {
        "motleyfins" {
            sourceSet sourceSets.main
        }
    }
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var expandProps = [
            'version'                      : version,
            'group'                        : project.group,
            'minecraft_version'            : minecraft_version,
            'minecraft_version_range'      : minecraft_version_range,
            'fabric_version'               : fabric_version,
            'fabric_loader_version'        : fabric_loader_version,
            'mod_name'                     : mod_name,
            'mod_author'                   : mod_author,
            'mod_id'                       : mod_id,
            'license'                      : license,
            'description'                  : project.description,
            'neoforge_version'             : neoforge_version,
            'neoforge_loader_version_range': neoforge_loader_version_range,
            'mod_group_id': mod_group_id,
            'credits'                      : credits,
            'java_version'                 : java_version
    ]
    expand expandProps
    from "src/main/resources/META-INF"
    into "build/generated/sources/modMetadata"
}

neoForge.ideSyncTask generateModMetadata

sourceSets.main.resources { srcDir generateModMetadata }

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'neoforge')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName, it.getTaskName(null, 'jarJar')].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'neoforge')
            }
        }
    }
}